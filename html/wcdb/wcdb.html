<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>liewli.io</title>
    <link rel="stylesheet"
      href="../static/default.min.css">
<script src="../static/highlight.min.js"></script>
</head>
<body>
    <h1>WCDB Overview - Part 1</h1>
<p>In this article, we will take a close look at <a href="https://github.com/Tencent/wcdb">WCDB</a>, an efficient, complete, easy-to-use mobile database framework built by WeChat. We focus mainly on it's ORM and WINQ subsystem, by digging into the source code, trying to understand how it is implemented.</p>
<h2>WCDB Usage</h2>
<p>To use WCDB create table in database, we should first define our business model, e.g.</p>
<pre><code class="language-objc">@interface Employee : NSObject
@property (nonatomic, assign) int ID;
@property (nonatomic, strong) NSString *name;
@property (nonatomic, assign) int age;
@end

//WCTTableCoding
@interface Employee () &lt;WCTTableCoding&gt;
WCDB_PROPERTY(ID)
WCDB_PROPERTY(name)
WCDB_PROPERTY(age)
@end


@implementation Employee

WCDB_IMPLEMENTATION(Employee)

WCDB_SYNTHESIZE(Employee, ID)
WCDB_SYNTHESIZE(Employee, name)
WCDB_SYNTHESIZE(Employee, age)

WCDB_PRIMARY_ASC_AUTO_INCREMENT(Employee, ID)

- (NSString *)description {
    return [NSString stringWithFormat:@&quot;&lt;Employee&gt;: %d %@ %d&quot;, self.ID, self.name, self.age];
}
@end

</code></pre>
<p>Note that out biz model should conforms to <code>WCTTableCoding</code> protocol, actually it's automatically synthesized by the macro <code>WCDB_PROPERTY</code>, <code>WCDB_SYNTHESIZE</code>.</p>
<pre><code class="language-objc">@protocol WCTTableCoding
@required
+ (const WCTBinding *)objectRelationalMappingForWCDB;
+ (const WCTPropertyList &amp;)AllProperties;
+ (const WCTAnyProperty &amp;)AnyProperty;
+ (WCTPropertyNamed)PropertyNamed; //className.PropertyNamed(propertyName)
@optional
@property(nonatomic, assign) long long lastInsertedRowID;
@property(nonatomic, assign) BOOL isAutoIncrement;
@end
</code></pre>
<p>After that, we can create our table based on our model definition</p>
<pre><code class="language-objc">WCTDatabase *database = [[WCTDatabase alloc] initWithPath:path]
BOOL ret =  [database createTableAndIndexesOfName:tableName withClass:Employee.class];
</code></pre>
<h3>CRUD</h3>
<p>WCDB make CRUD operation quite easy, take out <code>Employee</code> table as an example</p>
<ol>
<li>insert
<pre><code class="language-objc">  { // insert objs
    
    Employee *e = [Employee new];
    e.name = @&quot;Paul&quot;;
    e.age = 32;
    e.isAutoIncrement = YES;
    
    Employee *e1 = [Employee new];
    e1.name = @&quot;Allen&quot;;
    e1.age = 25;
    e1.isAutoIncrement = YES;
    
    
    WCTInsert *insert  = [database prepareInsertObjectsOfClass:Employee.class into:tableName];
    
    
    BOOL ret = [insert executeWithObjects:@[e, e1]];
    if (!ret) {
        NSLog(@&quot;inset objs error: %@&quot;, insert.error);
    }
}
</code></pre>
</li>
<li>select
<pre><code class="language-objc"> { //select objects
     WCTSelect *select = [database prepareSelectObjectsOfClass:Employee.class fromTable:tableName];
     
     NSArray *objects = select.allObjects;
     for (Employee *ee in objects) {
         NSLog(@&quot;%@&quot;, ee);
     }
 }
</code></pre>
output:
<pre><code> &lt;Employee&gt;: 1 Paul 32
 &lt;Employee&gt;: 2 Allen 25
</code></pre>
</li>
<li>update
<pre><code class="language-objc"> { //update by objects
     WCTUpdate *update = [[database prepareUpdateTable:tableName onProperties:Employee.age] where:Employee.name == @&quot;Paul&quot;];
     Employee *ee = [Employee new];
     ee.age = 33;
     BOOL ret = [update executeWithObject:ee];
     if (!ret) {
         NSLog(@&quot;Update by object Error: %@&quot;, update.error);
     }
 }
 
</code></pre>
output:
<pre><code class="language-objc"> &lt;Employee&gt;: 1 Paul 33
 &lt;Employee&gt;: 2 Allen 25
</code></pre>
</li>
<li>delete 
<pre><code class="language-objc">
{ //update by objects
    WCTDelete *deletion = [[database prepareDeleteFromTable:tableName] where:Employee.name == @&quot;Paul&quot;];
    BOOL ret = [deletion execute];
    if (!ret) {
        NSLog(@&quot;Delete Error %@&quot;, deletion.error);
    }
}
</code></pre>
output:
<pre><code class="language-objc">&lt;Employee&gt;: 2 Allen 25
</code></pre>
</li>
</ol>
<h2>ORM</h2>
<p>ORM (Object Relational Mapping), ORM is actually the process of binding, binding our biz domain model to our database model, the automatic binding takes place inside the macros we see before <code>WCDB_IMPLEMENTATION</code>, <code>WCDB_SYNTHESIZE</code>, <code>WCDB_PRIMARY_ASC_AUTO_INCREMENT</code>, <code>WCDB_PROPERTY</code>.   What does our <code>Employee</code> model looks like when we preprocessed these macros? </p>
<pre><code class="language-objc">@interface Employee () &lt;WCTTableCoding&gt;
+ (const WCTProperty &amp;)ID;
+ (const WCTProperty &amp;)name;
+ (const WCTProperty &amp;)age;
@end


@implementation Employee

static WCTBinding _s_Employee_binding(Employee.class);
static WCTPropertyList _s_Employee_properties;
+ (const WCTBinding *)objectRelationalMappingForWCDB {
  if (self.class != Employee.class) {
    WCDB::Error::Abort(&quot;Inheritance is not supported for ORM&quot;);
  }
  return &amp;_s_Employee_binding;
}
+ (const WCTPropertyList &amp;)AllProperties {
  return _s_Employee_properties;
}
+ (const WCTAnyProperty &amp;)AnyProperty {
  static const WCTAnyProperty s_anyProperty(Employee.class);
  return s_anyProperty;
}
+ (WCTPropertyNamed)PropertyNamed {
  return WCTProperty::PropertyNamed;
}

+ (const WCTProperty &amp;)ID {
  static const WCTProperty s_property(
      &quot;ID&quot;, Employee.class,
      _s_Employee_binding.addColumnBinding&lt;decltype([Employee new].ID)&gt;(&quot;ID&quot;,
                                                                        &quot;ID&quot;));
  return s_property;
}
static const auto _unused0 = [](WCTPropertyList &amp;propertyList) {
  propertyList.push_back(Employee.ID);
  return nullptr;
}(_s_Employee_properties);
+ (const WCTProperty &amp;)name {
  static const WCTProperty s_property(
      &quot;name&quot;, Employee.class,
      _s_Employee_binding.addColumnBinding&lt;decltype([Employee new].name)&gt;(
          &quot;name&quot;, &quot;name&quot;));
  return s_property;
}
static const auto _unused1 = [](WCTPropertyList &amp;propertyList) {
  propertyList.push_back(Employee.name);
  return nullptr;
}(_s_Employee_properties);
+ (const WCTProperty &amp;)age {
  static const WCTProperty s_property(
      &quot;age&quot;, Employee.class,
      _s_Employee_binding.addColumnBinding&lt;decltype([Employee new].age)&gt;(
          &quot;age&quot;, &quot;age&quot;));
  return s_property;
}
static const auto _unused2 = [](WCTPropertyList &amp;propertyList) {
  propertyList.push_back(Employee.age);
  return nullptr;
}(_s_Employee_properties);

@synthesize isAutoIncrement;
@synthesize lastInsertedRowID;
static const auto _unused3 = [](WCTBinding *binding) {
  binding-&gt;getColumnBinding(Employee.ID)
      -&gt;makePrimary(WCTOrderedAscending, true, WCTConflictNotSet);
  return nullptr;
}(&amp;_s_Employee_binding);

- (NSString *)description {
  return [NSString
      stringWithFormat:@&quot;&lt;Employee&gt;: %d %@ %d&quot;, self.ID, self.name, self.age];
}

@end

</code></pre>
<p>WCDB creates the table binding and column properties behind the scene.</p>
<h2>WINQ</h2>
<p>WINQ(WCDB language integrated query). WINQ is quite similar to LINQ in .net platform. With WINQ, we don't need to write glue code to concat sql query strings. Let's take a deep look at our <code>delete</code> operation, to figure out how it is implemented behind  the scene.</p>
<pre><code class="language-objc">{ //update by objects
    WCTUpdate *update = [[database prepareUpdateTable:tableName onProperties:Employee.age] where:Employee.name == @&quot;Paul&quot;];
    Employee *ee = [Employee new];
    ee.age = 33;
    BOOL ret = [update executeWithObject:ee];
    if (!ret) {
        NSLog(@&quot;Update by object Error: %@&quot;, update.error);
    }
}
</code></pre>
<pre><code class="language-objc">[[database prepareUpdateTable:tableName onProperties:Employee.age] where:Employee.name == @&quot;Paul&quot;];
</code></pre>
<p>Create the <code>WCTUpdate</code> statement which instantiated with <code>WCTPropertyList</code> , in this case, <code>Employee.age</code>. As we shown before, <code>Employee.age</code> is actually a <code>WCTProperty</code> generated by the macro <code>WCDB_SYNTHESIZE</code>.</p>
<pre><code class="language-objc">+ (const WCTProperty &amp;)age {
  static const WCTProperty s_property(
      &quot;age&quot;, Employee.class,
      _s_Employee_binding.addColumnBinding&lt;decltype([Employee new].age)&gt;(
          &quot;age&quot;, &quot;age&quot;));
  return s_property;
}
</code></pre>
<p>The <code>where</code> clause <code>Employee.name == @&quot;Paul&quot;</code> builds the <code>WCTExpr</code>, combined with the <code>where</code> condition, what we actually get is</p>
<pre><code class="language-objc">StatementUpdate &amp;StatementUpdate::where(const Expr &amp;where)
{
    if (!where.isEmpty()) {
        m_description.append(&quot; WHERE &quot; + where.getDescription());
    }
    return *this;
}
</code></pre>
<p><code>m_description</code> is actually the serialized SQL query string. <code>where</code> expr get serialized into 
<code>(age == 33)</code>.</p>
<p><code>[deletion execute]</code> makes the final execution call</p>
<pre><code class="language-objc">- (BOOL)execute
{
    WCDB::ScopedTicker scopedTicker(_ticker);
    WCDB::RecyclableStatement statementHandle = _core-&gt;prepare(_statement, _error);
    if (!statementHandle) {
        return NO;
    }
    statementHandle-&gt;step();
    if (!statementHandle-&gt;isOK()) {
        _error = statementHandle-&gt;getError();
        return NO;
    }
    _changes = statementHandle-&gt;getChanges();
    return YES;
}
</code></pre>
<p>The <code>_core-&gt;prepare(_statement, _error)</code> creates the desired sqlite prepared statement</p>
<pre><code class="language-objc">std::shared_ptr&lt;StatementHandle&gt; Handle::prepare(const Statement &amp;statement)
{
    if (statement.getStatementType() == Statement::Type::Transaction) {
        Error::Abort(
            &quot;[prepare] a transaction is not allowed, use [exec] instead&quot;,
            &amp;m_error);
        return nullptr;
    }
    sqlite3_stmt *stmt = nullptr;
    int rc = sqlite3_prepare_v2((sqlite3 *) m_handle,
                                statement.getDescription().c_str(), -1, &amp;stmt,
                                nullptr);
    if (rc == SQLITE_OK) {
        m_error.reset();
        return std::shared_ptr&lt;StatementHandle&gt;(
            new StatementHandle(stmt, *this));
    }
    Error::ReportSQLite(m_tag, path, Error::HandleOperation::Prepare, rc,
                        sqlite3_extended_errcode((sqlite3 *) m_handle),
                        sqlite3_errmsg((sqlite3 *) m_handle),
                        statement.getDescription(), &amp;m_error);
    return nullptr;
}
</code></pre>
<p>The <code>step</code> and is an thin wrapper of <code>sqlite3_step</code>.</p>
<pre><code class="language-objc">bool StatementHandle::step()
{
    int rc = sqlite3_step((sqlite3_stmt *) m_stmt);
    if (rc == SQLITE_ROW || rc == SQLITE_OK || rc == SQLITE_DONE) {
        m_error.reset();
        return rc == SQLITE_ROW;
    }
    sqlite3 *handle = sqlite3_db_handle((sqlite3_stmt *) m_stmt);
    Error::ReportSQLite(
        m_handle.getTag(), m_handle.path, Error::HandleOperation::Step, rc,
        sqlite3_extended_errcode(handle), sqlite3_errmsg(handle),
        sqlite3_sql((sqlite3_stmt *) m_stmt), &amp;m_error);
    return false;
}
</code></pre>
<p>We have walk through the entire process of WINQ operation. The whole process chain can be summarized as </p>
<pre><code>WCTUpdate -&gt; Serialization -&gt; Prepared Statement -&gt; Execution
</code></pre>
<p>The other <code>CRUD</code> operations are quite similar, WCDB provides <code>WCTInset</code>, <code>WCTSelect</code>, <code>WCTDelete</code>, etc. for CURD chain call. We can build complex SQL queries without too much effort.</p>
<h2>Insights</h2>
<p>WCDB provides ORM &amp; WINQ facilities to provide easy manipulation of underlying sqlite database. Actually provides much more capabilities such as multi-thread concurrency access, encryption, corruption recovery, and anti-injection. We will discuss this in the following series.</p>

    <script>hljs.highlightAll();</script>
</body>
</html>